package(default_visibility = ["//visibility:public"])

load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("//deploy/helm/kubecf:defs.bzl", "metadata_file_generator")
load(
    "//rules/helm:def.bzl",
    helm_package = "package",
    helm_version = "version",
)
load("//rules/yaml_extractor:defs.bzl", "yaml_extractor")
load("//:def.bzl", "project")

metadata_file_generator(
    name = "metadata",
    file = "Metadata.yaml",
    operator_chart = project.external_files["cf_operator"].urls[0].format( version = project.external_files["cf_operator"].version )
)

filegroup(
    name = "chart_files_static",
    srcs = glob(
        ["**/*"],
        exclude = [
            "**/BUILD.bazel",
            "**/defs.bzl",
        ],
    ),
)

pkg_tar(
    name = "cf_deployment",
    package_dir = "assets",
    srcs = [
        "@cf_deployment//:cf_deployment",
    ],
)

yaml_extractor(
    name = "auctioneer_job",
    src = "@cf_deployment//:cf-deployment.yml",
    filter = """.instance_groups[] | select(.name == "scheduler") | .jobs[] | select(.name == "auctioneer")""",
)

yaml_extractor(
    name = "routing_api_job",
    src = "@cf_deployment//:cf-deployment.yml",
    filter = """.instance_groups[] | select(.name == "api") | .jobs[] | select(.name == "routing-api")""",
)

pkg_tar(
    name = "extracted_jobs",
    package_dir = "assets/jobs",
    srcs = [
        ":auctioneer_job",
        ":routing_api_job",
    ],
)

helm_package(
    name = "kubecf",
    srcs = [
        ":chart_files_static",
    ],
    generated = [
        ":metadata",
    ],
    tars = [
        "//bosh/releases:pre_render_scripts",
        ":cf_deployment",
        ":extracted_jobs",
    ],
)

helm_version(
    name = "version",
    chart = ":kubecf",
)
